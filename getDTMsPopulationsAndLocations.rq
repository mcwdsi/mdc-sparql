PREFIX obc: <http://www.pitt.edu/obc/>
PREFIX obo: <http://purl.obolibrary.org/obo/>

select distinct ?compiledSoftware ?compiledSoftwareLabel ?compiledSoftwarePrefTerm ?hostPopulation ?hostPopulationLabel ?hostType ?hostTypeLabel ?pathogenPopulation ?pathogenPopulationLabel ?pathogenType ?pathogenTypeLabel ?geoLocation ?geoLocationLabel ?controlMeasure ?controlMeasureLabel

from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
from <http://www.pitt.edu/obc/simulates-instances>

WHERE {

#get compiled software that achieves planned objective simulating of process type x
	?simulatingOfProcessTypeX obo:OBI_0000417 ?compiledSoftware . #achieves planned objective obo:OBI_0000417
	?compiledSoftware rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000525 . 
	?simulatingOfProcessTypeX rdf:type obo:APOLLO_SV_00000005 .
#get simulation of populations
	?simulatingOfProcessTypeX obo:OBI_0000299 ?simulationOfPopulation .   #has specified output - obo:OBI_0000299
  	?simulationOfPopulation rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000031  .
  	?simulationOfPopulation obo:APOLLO_SV_00000067 ?population .
  	?population rdf:type/rdfs:subClassOf* obo:PCO_0000001 . #PCO_0000001 population  .

#If the compiled software has a label, get the label.
OPTIONAL {?compiledSoftware rdfs:label ?compiledSoftwareLabel .} 

#If the compiled software has a preferred term, get it.
OPTIONAL {?compiledSoftware obo:IAO_0000111 ?compiledSoftwarePrefTerm .} 

#If available, get (1) the host populations that are simulated, (2) their labels, (3) the type of host, and (4) the label for the host type.
OPTIONAL {
 	?population rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000516 .
	?population rdfs:label ?hostPopulationLabel .
  	?population a ?hostType .
	?hostType rdfs:label ?hostTypeLabel .
}

#If available, get (1) the pathogen populations that are simulated, (2) their labels, (3) the type of pathogens, and (4) the label for the pathogen type.
OPTIONAL {
   	?population rdf:type/rdfs:subClassOf* obc:IDE_0000000007 .
    ?population rdfs:label ?pathogenPopulationLabel .
	?population a ?pathogenType .
	?pathogenType rdfs:label ?pathogenTypeLabel .
}

#If available, get the location.
OPTIONAL {
 #get the biotic ecosystem of the population
 ?population obo:BFO_0000137 ?bioticEcosystem . #BFO_0000137 proper part of continuant at all times
 ?bioticEcosystem rdf:type obo:APOLLO_SV_00000104 .
 #get the ecosystm that contains the biotic ecosystem
 	#obo:ecosystem APOLLO_SV_00000097
 ?bioticEcosystem obo:BFO_0000137 ?ecosystem . #BFO_0000137 proper part of continuant at all times
 ?ecosystem rdf:type obo:APOLLO_SV_00000097 .
 #get the geographic region/location of the ecosystem.
 ?ecosystem <http://www.obofoundry.org/ro/ro.owl#located_in> ?geoLocation .
 ?geoLocation rdfs:label ?geoLocationLabel
}
  
#If available, get the control measure.
OPTIONAL {
	?simulatingOfProcessTypeX obo:OBI_0000299 ?controlMeasure .   #has specified output - obo:OBI_0000299
  	?controlMeasure rdf:type/rdfs:subClassOf* obo:APOLLO_SV_00000086 . #control strategy -  obo:APOLLO_SV_00000086
  	?controlMeasure rdfs:label ?controlMeasureLabel
}


}
ORDER BY ?compiledSoftware