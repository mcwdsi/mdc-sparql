prefix obo: <http://purl.obolibrary.org/obo/>
prefix dc: <http://purl.org/dc/elements/1.1/>

select (count (distinct ?x ) as ?numberDataFormatsWithDoi)
from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
where {
 #the item (?x) is in the MIDAS Digital Commons
  <http://www.pitt.edu/obc/IDE_ARTICLES_12000070095> obo:BFO_0000174 ?x  .

  ?x rdf:type ?dataFormatOrSubclass .
  
  #the item is a data format specification
  ?dataFormatOrSubclass rdfs:subClassOf* obo:IAO_0000098 .

  #there is a ?z that is an instance of DOI that denotes ?x
  ?z rdf:type obo:OBI_0002110 .
  ?z obo:IAO_0000219 ?x .

  #filter out punned individuals
  FILTER ( ?x != obo:IAO_0000098 )
  FILTER ( ?x != ?dataFormatOrSubclass )
}