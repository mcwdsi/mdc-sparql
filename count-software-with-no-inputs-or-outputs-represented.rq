prefix obo: <http://purl.obolibrary.org/obo/>

select (count (distinct ?x) as ?numberSoftwareWithNoInputOrOutputObjectsRepresented) #distinct ?x ?xLabel ?parsingOrEncodingPlan 
from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
where {
  {
	#software must be in MDC
  	<http://www.pitt.edu/obc/IDE_ARTICLES_12000070095> obo:BFO_0000174 ?x  .
	  
    ?x rdf:type ?softwareOrSubclass .
    ?x rdfs:label ?xLabel .
  	?softwareOrSubclass rdfs:subClassOf* obo:IAO_0000010 .

 
  }
  #filter out punned individuals
     FILTER ( ?x != obo:IAO_0000010 )
     FILTER ( ?x != ?softwareOrSubclass )
     FILTER ( ?softwareOrSubclass != obo:APOLLO_SV_00000525 )
  
  	 #and here is the magic.  We filter out any result where there is no data format realized at all
     FILTER NOT EXISTS {  
         	?v rdf:type obo:APOLLO_SV_00000525 .  #v is executable
  			?u rdf:type obo:APOLLO_SV_00000524 .  #u is compiling
  			?u obo:OBI_0000293 ?x .               #compiling has source code as input
  			?u obo:OBI_0000299 ?v .               #compiling has executable as output
   
       	?parsingOrEncodingPlan obo:BFO_0000050 ?v .		 #data parsing or encoding plan specification is part of executable
        ?dataParsingOrEncoding obo:OBI_0000417 ?parsingOrEncodingPlan . #parsing or encoding achieves parsing plan objective
       	?dataParsingOrEncoding obo:OBI_0000417 ?formatSpec .  #parsing or encoding achieves spec objective
	  	?formatSpec rdf:type obo:IAO_0000098 .   #spec is in an instance of data format
     }
}
order by ?xLabel