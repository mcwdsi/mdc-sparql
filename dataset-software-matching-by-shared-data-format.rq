prefix obo: <http://purl.obolibrary.org/obo/>
prefix dc: <http://purl.org/dc/elements/1.1/>

select ?dataset ?datasetName ?datasetTitle ?datasetLabel ?datasetFormatAndSoftwareInputFormat ?x1 ?softwareOne
#select distinct ?dataset

from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/obc/mdc/content>
where {
#Overall goal: dataset has a format that matches input spec of a software

  ?dataset rdf:type ?ds .
  ?ds rdfs:subClassOf* obo:IAO_0000100 .  #?ds is an instance of dataset
  ?p obo:OBI_0000299 ?dataset . #the dataset is the specified output of p
  ?p obo:OBI_0000417 ?formatSpec . #p achieves planned objective ?inputSpec
  ?dataset obo:IAO_0000111 ?datasetName .   #?dataset has a preferred term
  OPTIONAL {?dataset rdfs:label ?datasetLabel} .
  OPTIONAL {?dataset dc:title ?datasetTitle } .

#The software has an input spec of ?x
#And we want this software to have the dataset spec as its inputSpec  
  ?x1 rdf:type ?y1 .
  ?y1 rdfs:subClassOf* obo:IAO_0000010 .    #?x1 is some instance of software
  ?x1 obo:IAO_0000111 ?softwareOne .   #?x1 has a preferred term
  ?v1 rdf:type obo:APOLLO_SV_00000525 .  #v1 is executable form x1
  ?u1 rdf:type obo:APOLLO_SV_00000524 .  #u1 is compiling of x1 to v1
  ?u1 obo:OBI_0000293 ?x1 .               #compiling has source code as input
  ?u1 obo:OBI_0000299 ?v1 .               #compiling has executable as output
  ?parsingPlan obo:BFO_0000050 ?v1 .    #data encoding plan spec part of executable
  ?dataParsing obo:OBI_0000417 ?parsingPlan . #encoding achieves encoding plan objective
  ?dataParsing obo:OBI_0000417 ?formatSpec .  #encoding achieves spec objective, this is spec of dataset
  ?dataParsing rdf:type obo:APOLLO_SV_00000566 .  #encoding individual is instance of encoding

  ?formatSpec rdfs:label ?datasetFormatAndSoftwareInputFormat . #and the input spec has a label hopefully

#and the dataset and the software must both be in the MDC (for now)
  <http://www.pitt.edu/obc/IDE_ARTICLES_12000070095> obo:BFO_0000174 ?dataset  .
  <http://www.pitt.edu/obc/IDE_ARTICLES_12000070095> obo:BFO_0000174 ?x1  .

}