prefix obo: <http://purl.obolibrary.org/obo/>

select ?x ?prefTerm ?title ?sourceCodeRelease ?version ?doi ?doiURL ?synopsis ?sourceCodeRepositoryUrl ?executableName ?executableUrl ?webAppUrl ?licenseName ?licenseUrl ?documentationDescription ?documentationUrl ?websiteUrl ?developerName ?pubAbout ?pubThatUsedRelease
from <http://www.pitt.edu/obc/ide/apollo-sv>
from <http://www.pitt.edu/obc/ide/classes-and-indexing-instances>
from <http://www.pitt.edu/obc/ide/article-instances>
from <http://www.pitt.edu/obc/ide/article-deleted-instances>
from <http://ide.obc.io/auth>
from <http://www.pitt.edu/mdc/content>
where {
  ?x rdf:type <http://purl.obolibrary.org/obo/APOLLO_SV_00000174> .
  ?x obo:IAO_0000111 ?prefTerm .
  #if there is a title annotation, get the title
 	OPTIONAL { ?x dc:title ?title } .
  #get URL to source code for this version if present
  	OPTIONAL { ?x obo:ERO_0000480 ?sourceCodeRelease } .
  #get version if present
  	OPTIONAL { ?y rdf:type obo:APOLLO_SV_00000108 .
               ?y obo:IAO_0000219 ?x .
               ?y rdfs:label ?version } .
  #get DOI if present
  	OPTIONAL { ?z rdf:type obo:OBI_0002110 .
               ?z obo:IAO_0000219 ?x .
               ?z rdfs:label ?doi } .
  #get DOI URL if present
  OPTIONAL { ?z rdf:type obo:OBI_0002110 .
               ?z obo:IAO_0000219 ?x .
               ?z obo:ERO_0000480 ?doiURL } .
  #get human readable synopsis if present
  	OPTIONAL { ?x obo:APOLLO_SV_00000523 ?synopsis } .
  #get source code repository information if present
  	OPTIONAL { ?w rdf:type obo:APOLLO_SV_00000522 .
               ?x obo:BFO_0000050 ?w .
               ?w obo:ERO_0000480 ?sourceCodeRepositoryUrl } .
  #get executable(s) name if present
  	OPTIONAL { ?v rdf:type obo:APOLLO_SV_00000525 .
  			   ?u rdf:type obo:APOLLO_SV_00000524 . 
  			   ?u obo:OBI_0000293 ?x . 
  			   ?u obo:OBI_0000299 ?v . 
  			   ?v rdfs:label ?executableName } .
  #get executable(s) url if present
   	OPTIONAL { ?v rdf:type obo:APOLLO_SV_00000525 .
  			   ?u rdf:type obo:APOLLO_SV_00000524 . 
  			   ?u obo:OBI_0000293 ?x . 
  			   ?u obo:OBI_0000299 ?v . 
  			   ?v obo:ERO_0000480 ?executableUrl } .
  #get web application url if present
  	OPTIONAL { ?v rdf:type obo:APOLLO_SV_00000525 . 
  			   ?u rdf:type obo:APOLLO_SV_00000524 . 
  			   ?u obo:OBI_0000293 ?x .
  			   ?u obo:OBI_0000299 ?v . 
  			   ?v obo:BFO_0000054 ?n . 
  			   ?n obo:ERO_0000480 ?webAppUrl } .
  #get license name if present
  	OPTIONAL { ?t rdf:type obo:APOLLO_SV_00000535 . 
  			   ?t obo:BFO_0000050 ?x . 
  			   ?t rdfs:label ?licenseName } .
  #get license URL if present
   	OPTIONAL { ?t rdf:type obo:APOLLO_SV_00000535 . 
  			   ?t obo:BFO_0000050 ?x . 
  			   ?t obo:ERO_0000480 ?licenseUrl } .
  #get documentation description if present
  	OPTIONAL { ?s rdf:type obo:IAO_0000310 .
  			   ?s obo:IAO_0000136 ?x .
  			   ?s rdfs:label ?documentationDescription } .
  #get documentation URL if present
  	OPTIONAL { ?s rdf:type obo:IAO_0000310 .
  			   ?s obo:IAO_0000136 ?x .
  			   ?s obo:ERO_0000480 ?documentationUrl } .
  #get website URL if present
  	OPTIONAL { ?o rdf:type obo:FBcv_0000667 . 
  			   ?o obo:IAO_0000136 ?x . 
  			   ?o obo:ERO_0000480 ?websiteUrl } .
  #get developer(s) if any
  	OPTIONAL { ?r rdf:type obo:BFO_0000040 . 
  			   ?r obo:RO_0000053 ?q . 
  			   ?q rdf:type obo:OMRSE_00000038 . 
  			   ?p rdf:type obo:APOLLO_SV_00000008 . 
  			   ?p obo:RO_0002218 ?r . 
  			   ?p obo:OBI_0000299 ?x . 
  			   ?r rdfs:label ?developerName } .
  #get publication(s) about if any
  	OPTIONAL { ?n obo:IAO_0000136 ?x .
               ?n rdf:type obo:IAO_0000311 .
               ?n rdfs:label ?pubAbout } .
  #get publication(s) that used release if any
  	OPTIONAL { ?m obo:IAO_0000136 ?k .               #m is about k
  			   ?k rdf:type obo:OBI_0000471 .		 #k is instance of study
  			   ?m rdf:type obo:IAO_0000311 . 		 #m is instance of pub
  			   ?m dc:title ?pubThatUsedRelease .   #m has label pubThatUsedRelease
  			   ?i obo:BFO_0000054 ?j .				#i realized by j
  			   ?j rdf:type obo:OBIws_0000050 .		#j is instance of code execution
  			   ?j obo:BFO_0000050 ?k .				#j (execution) part of k (study)
  			   ?i rdf:type obo:APOLLO_SV_00000525 .	#i instance of executable
  			   ?h obo:OBI_0000299 ?i .				#i specified output of h
  			   ?h rdf:type obo:APOLLO_SV_00000524 .	#h instance of compiling
  			   ?h obo:OBI_0000293 ?x				#h has input the dtm 
             } .
  #filter out punned individuals
     FILTER ( ?x != obo:APOLLO_SV_00000174 )
     }